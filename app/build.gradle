apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply from: '../dependencies.gradle'
apply from: '../helpers.gradle'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.bmacedo.hiremenews"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'com.github.tmurakami.dexopener.DexOpenerAndroidJUnitRunner'
        multiDexEnabled true
        resValue 'string', "BASE_URL", "\"https://newsapi.org/\""
        resValue 'string', "NEWS_API_KEY", "\"${getNewsApiKey().trim()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kapt {
        correctErrorTypes = true
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation supportLibs
    implementation networkLibs
    implementation rxJavaLibs
    implementation firebase
    implementation otherLibs
    debugImplementation debugLibs
    releaseImplementation releaseLibs
    kapt annotationProcessorLibs
    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs
}

apply plugin: 'com.google.gms.google-services'
